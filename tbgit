#!/usr/bin/perl -w
use strict;

use FindBin;
use Getopt::Long;
use IO::CaptureOutput qw(capture_exec);
use File::Slurp qw(read_file write_file);
use File::Basename;
use File::Find;
use File::Temp;
use Cwd qw(getcwd);
use LWP::Simple;
use JSON 'decode_json';

my $docdir = "$FindBin::Bin/doc";
my $hgdir = "$FindBin::Bin/hg_clones";
my $mapfile = "$FindBin::Bin/gecko-mapfile";
my $default_trychooser = 'try: -b d -p linux,linux64,linux64-st-an -u reftest,crashtest,xpcshell,jsreftest,marionette,cppunit,jittests,mochitest-1,mochitest-2,mochitest-3,mochitest-4,mochitest-5,mochitest-bc -t none';

my $exit_branch;
my %options;
my %actions = (
    tbgit => {
        cmd => sub {},
        options => [],
    },
    help => {
        cmd => \&cmd_help,
        options => [],
    },
    merge => {
        cmd =>\&cmd_merge,
        options => [ 'list', 'yes', 'no-edit', ],
    },
    pull => {
        cmd => \&cmd_pull,
        options => [ 'list', 'yes', 'no-fetch', ],
    },
    push => {
        cmd => \&cmd_push,
        options => [ 'list', 'yes', 'no-fetch', ],
    },
    'try-results' => {
        cmd => \&cmd_tryresults,
        options => [ 'failed', 'success', 'details', ],
    },
    'try-push' => {
        cmd => \&cmd_trypush,
        options => [ 'list', 'yes', 'max-push=i', ],
    },
    'try-config' => {
        cmd => \&cmd_tryconfig,
        options => [ 'edit', 'add=s', 'show', 'remove', ],
    },
    'list-branches' => {
        cmd => \&cmd_list_branches,
        options => [],
    },
    'rebase' => {
        cmd => \&cmd_rebase,
        options => [ 'list', 'yes', 'continue', 'abort', ],
    },
);

sub exit_error {
    print STDERR "Error: ", $_[0], "\n";
    if ($exit_branch && (!git_current_branch()
            || $exit_branch ne git_current_branch())) {
        capture_exec('git', 'checkout', $exit_branch);
    }
    exit (exists $_[1] ? $_[1] : 1);
}

sub ask_yn {
    my ($msg, $default) = @_;
    my $yn = $default ? $default eq 'y' ? '[Y/n]' : '[y/N]' : '[y/n]';
    print "$msg $yn\n";
    while (my $z = <STDIN>) {
        chomp $z;
        if ($z eq '' && $default) {
            $z = $default;
        }
        if ($z eq 'y' || $z eq 'Y') {
            return 1;
        }
        if ($z eq 'n' || $z eq 'N') {
            return undef;
        }
        print "$msg $yn\n";
    }
    return undef;
}

sub cmd_help {
    my ($cmd) = @_;
    $cmd = 'help' if @_ > 1;
    $cmd = 'tbgit' if @_ == 0;
    exit_error("Unknown commmand $cmd") unless $actions{$cmd};
    print read_file "$docdir/$cmd.txt";
    exit 0;
}

sub run_noerr {
    my @cmd = @_;
    if ($options{'dry-run'}) {
        print "dry-run: ", join(' ', @cmd);
        return;
    }
    exit_error('Failed to run: ' . join(' ', @cmd)) unless system(@cmd) == 0;
}

sub run_noerr_noout {
    my @cmd = @_;
    my (undef, $err, $success) = capture_exec(@cmd);
    exit_error('Failed to run: ' . join(' ', @cmd) . "\n$err") unless $success;
}

sub run_noerr_ask {
    if (!$options{yes}) {
        return unless ask_yn("Run '" . join(' ', @_) . '\'?', 'n');
    }
    run_noerr(@_);
}

sub git_dir {
    for (my $dir = getcwd; $dir ne '/'; $dir = dirname($dir)) {
        return "$dir/.git" if -d "$dir/.git";
    }
    exit_error("Not in a git repository");
}

sub git_current_branch {
    my $d = read_file(git_dir . '/HEAD');
    exit_error "Cannot read HEAD" unless $d;
    return $d =~ m/ref: refs\/heads\/(.+)$/ ? $1 : undef;
}

sub get_versions {
    my $branch = git_current_branch;
    exit_error "Cannot parse versions in current branch" unless $branch;
    if ($branch =~ m/^tor-browser-([^-]+)-([^-]+)-.+/) {
        return (1, $1, $2);
    }
    if ($branch =~ m/^([^\/]+)\/[^\/]+$/) {
        return (undef, $1, undef);
    }
    if ($branch =~ m/^([^\/]+)\/([^\/]+)\/[^\/]+$/) {
        return (undef, $1, $2);
    }
    exit_error "Cannot parse versions in current branch: $branch";
}

sub git_hash {
    my ($branch) = @_;
    my ($out, $err, $success) = capture_exec('git', 'show', '-s',
                                        '--format=%H', "$branch^{commit}");
    chomp $out;
    return $out;
}

sub git_commit_parents {
    my ($branch) = @_;
    my ($out, $err, $success) = capture_exec('git', 'show', '-s',
                                        '--format=%P', "$branch^{commit}");
    chomp $out;
    return split(' ', $out);
}

sub git_is_ancestor {
    my ($c1, $c2) = @_;
    my ($out, $err, $success) = capture_exec('git', 'merge-base',
                                             '--is-ancestor', $c1, $c2);
    return $success;
}

sub git_notes_get {
    my ($ref, $object) = @_;
    my ($out, $err, $success) = capture_exec('git', 'notes', '--ref',
                                             $ref, 'show', $object);
    return undef unless $success;
    chomp $out;
    return $out;
}

sub git_notes_get_option {
    my ($ref, $object, $opt) = @_;
    my $txt = git_notes_get($ref, $object);
    return () unless defined $txt;
    my @res;
    foreach my $line (split("\n", $txt)) {
        next unless $line =~ m/=/;
        my ($key, $val) = split('=', $line, 2);
        chomp $val;
        push @res, $val if $key eq $opt;
    }
    return @res;
}

sub git_notes_append {
    my ($ref, $object, $txt) = @_;
    run_noerr_noout('git', 'notes', '--ref', $ref, 'append', '-m', $txt, $object);
}

sub git_fetch {
    my ($repo) = @_;
    return if $options{'no-fetch'};
    my ($out, $err, $success) = capture_exec('git', 'fetch', $repo);
    exit_error "Error fetching $repo:\n$err" unless $success;
}

sub branch_exists {
    my %branches = map { $_ => 1 } branches_list('heads');
    return $branches{$_[0]};
}

sub branches_list {
    my ($refdir, $ffver, $tbver) = @_;
    my $hdir = git_dir . "/refs/$refdir";
    my @branches;
    my $wanted = sub {
        return unless -f $File::Find::name;
        my $branch = $File::Find::name;
        $branch =~ s/^$hdir\///;
        push @branches, $branch;
    };
    find($wanted, $hdir);
    if (-f git_dir . '/packed-refs') {
        foreach my $line (read_file(git_dir . '/packed-refs')) {
            if ($line =~ m/^[^ ]+ refs\/$refdir\/(.+)$/) {
                push @branches, $1;
            }
        }
    }
    return sort @branches unless $ffver;
    my %b;
    foreach my $branch (@branches) {
        $b{$1} = $branch if ($branch =~ m/^$ffver\/([^\/]+)$/);
    }
    return sort values %b unless $tbver;
    foreach my $branch (@branches) {
        $b{$1} = $branch if ($branch =~ m/^$ffver\/$tbver\/([^\/]+)$/);
    }
    return sort values %b;
}

sub wget_mapfile {
    run_noerr('wget', "-O$mapfile",
        'https://people.mozilla.org/~pmoore/vcs2vcs/gecko-dev/gecko-mapfile');
}

sub hg_git_commits_map {
    my @l = read_file($mapfile);
    my %r = map { (m/^([^ ]+) ([^ ]+)/) } @l;
    chomp %r;
    return \%r;
}

sub git_to_hg_hash {
    my ($git_hash) = @_;
    if (my @h = git_notes_get_option('try-push', $git_hash, 'hg_hash')) {
        return $h[0];
    }
    wget_mapfile() unless -f $mapfile;
    my $map = hg_git_commits_map();
    return $map->{$git_hash} if $map->{$git_hash};
    wget_mapfile();
    $map = hg_git_commits_map();
    exit_error "Could not find hg commit for $git_hash" unless $map->{$git_hash};
    return $map->{$git_hash};
}

sub hg_clone_init {
    my ($name) = @_;
    my $oldcwd = getcwd;
    mkdir $hgdir unless -d $hgdir;
    chdir $hgdir;
    run_noerr('hg', 'clone', '-v',
        "https://hg.mozilla.org/releases/$name", $name) unless -d $name;
    chdir $name;
    run_noerr_noout('hg', 'qpop', '--all');
    my ($unapplied) = capture_exec('hg', 'qunapplied');
    foreach my $u (split "\n", $unapplied) {
        chomp $u;
        run_noerr_noout('hg', 'qdelete', $u);
    }
    run_noerr_noout('hg', 'pull');
    chdir $oldcwd;
}

sub try_patch {
    my ($name, $git_branch, $git_base) = @_;
    my ($log) = capture_exec('git', 'log', "$git_base..$git_branch");
    my ($diff) = capture_exec('git', 'diff', "$git_base..$git_branch");
    my $file = File::Temp->new();
    write_file($file, "$name: $git_branch\n\n$log\n$diff");
    return $file;
}

sub get_esr_version {
    my ($branch) = @_;
    return $1 if $branch =~ m/^(\d+)\.\d+\.\d+esr\//;
    exit_error "Cannot get esr version from branch $branch";
}

sub push_to_try {
    my ($git_branch, $git_base, $try_branch) = @_;
    my $esr_version = get_esr_version($git_branch);
    my $trypatch = try_patch('Try', $try_branch, $git_base) if $try_branch;
    my $tbpatch = try_patch('Tor Browser', $git_branch, $git_base);
    my $hg_base = git_to_hg_hash(git_hash($git_base));
    hg_clone_init("mozilla-esr$esr_version");
    my $oldcwd = getcwd;
    chdir "$hgdir/mozilla-esr$esr_version";
    run_noerr_noout('hg', 'update', '-C', $hg_base);
    run_noerr_noout('hg', 'qimport', '-P', $trypatch->filename, '-n',
                    'try.patch') if $try_branch;
    run_noerr_noout('hg', 'qimport', '-P', $tbpatch->filename, '-n',
                    'torbrowser.patch');
    run_noerr_noout('hg', 'qnew', '-m', $default_trychooser, 'trychooser');
    my ($hg_id) = capture_exec('hg', 'id', '-i');
    chomp $hg_id;
    print "Pushing $hg_id to Try\n";
    run_noerr(qw(hg push -r tip -f ssh://hg.mozilla.org/try/));
    chdir $oldcwd;
    git_notes_append('try-push', $git_branch, "pushid=$hg_id");
}

sub cmd_merge {
    cmd_help('merge') unless @_ == 0;
    my ($mb, $ffver, $tbver) = get_versions;
    exit "This command should be run from a merge branch" unless $mb;
    my @branches = branches_list('heads', $ffver, $tbver);
    foreach my $branch (@branches) {
        next if $branch eq "$ffver/base.try";
        next if git_is_ancestor($branch, 'HEAD');
        if ($options{list}) {
            print "$branch\n";
            next;
        }
        run_noerr_ask('git', 'merge',
                      $options{'no-edit'} ? ('--no-edit') : (), '--log', $branch);
    }
}

sub cmd_pull {
    cmd_help('pull') unless @_ == 1;
    my ($remote) = @_;
    my ($mb, $ffver, $tbver) = get_versions;
    exit "This command should be run from a merge branch" unless $mb;
    git_fetch($remote);
    my @local_branches = branches_list("heads", $ffver, $tbver);
    my @remote_branches = branches_list("remotes/$remote", $ffver, $tbver);
    my %l = map { $_ => 1 } @local_branches;
    my %r = map { $_ => 1 } @remote_branches;
    foreach my $branch (@remote_branches) {
        if (!$l{$branch}) {
            print "$branch (new)\n";
        } elsif (!git_is_ancestor("$remote/$branch", $branch)) {
            if (git_is_ancestor($branch, "$remote/$branch")) {
                if ($options{list}) {
                    print "$branch (pull)\n";
                    next;
                }
                run_noerr_ask('git', 'update-ref', $branch, "$remote/$branch");
            } else {
                if ($options{list}) {
                    print "$branch (non-ff pull)\n";
                    next;
                }
                print "Warning: non fast-forward pull. You may lose some commits.\n";
                run_noerr_ask('git', 'update-ref', $branch, "$remote/$branch");
            }
        }
    }
    foreach my $branch (@local_branches) {
        if (!$r{$branch}) {
            if ($options{list}) {
                print "$branch (remove)\n";
                next;
            }
            print "Warning: removing branch $branch\n";
            run_noerr_ask('git', 'branch', '-D', $branch);
        }
    }
}

sub cmd_push {
    cmd_help('push') unless @_ == 1;
    my ($remote) = @_;
    my ($mb, $ffver, $tbver) = get_versions;
    exit "This command should be run from a merge branch" unless $mb;
    git_fetch($remote);
    my @local_branches = branches_list("heads", $ffver, $tbver);
    my @remote_branches = branches_list("remotes/$remote", $ffver, $tbver);
    my %l = map { $_ => 1 } @local_branches;
    my %r = map { $_ => 1 } @remote_branches;
    foreach my $branch (@remote_branches) {
        next unless $l{$branch};
        next if git_hash($branch) eq git_hash("$remote/$branch");
        if (git_is_ancestor("$remote/$branch", $branch)) {
            print "$branch (push)\n";
            next if $options{list};
            run_noerr_ask('git', 'push', $remote, "$branch:$branch");
        } else {
            print "$branch (non-ff push)\n";
            next if $options{list};
            print "Warning: non fast-forward push required on $branch\n";
        }
    }
    foreach my $branch (@local_branches) {
        next if $r{$branch};
        print "$branch (new)\n";
        next if $options{list};
        run_noerr_ask('git', 'push', $remote, "$branch:$branch");
    }
}

sub treeherder_get_result_set_id {
    my ($hg_hash) = @_;
    my $url = 'https://treeherder.mozilla.org/api/project/try/resultset/'
              . "?count=10&full=true&revision=$hg_hash";
    my $content = get($url);
    exit_error "Cannot get $url" unless $content;
    my $data = decode_json($content);
    exit_error "Error getting resultset id" unless $data->{results}->[0]->{id};
    return $data->{results}->[0]->{id};
}

sub treeherder_get_jobs {
    my ($hg_hash) = @_;
    my $resultset_id = treeherder_get_result_set_id($hg_hash);
    my $url = 'https://treeherder.mozilla.org/api/project/try/jobs/?count=2000&'
              . "result_set_id=$resultset_id&return_type=list";
    my $content = get($url);
    exit_error "Cannot get $url" unless $content;
    my $data = decode_json($content);
    my @jobs;
    return () if !$data->{job_property_names};
    my @prop_names = @{$data->{job_property_names}};
    foreach my $result (@{$data->{results}}) {
        my @res = @$result;
        my %job;
        foreach my $prop_name (@prop_names) {
            $job{$prop_name} = shift @res;
        }
        push @jobs, \%job;
    }
    return @jobs;
}

sub treeherder_jobs_by_status {
    my @jobs = @_;
    my %j;
    foreach my $job (@jobs) {
        next if $job->{job_group_symbol} eq 'R-e10s';
        next if $job->{result} eq 'retry';
        my $name;
        my $status;
        if ($job->{job_group_symbol} eq '?') {
            $name = $job->{job_type_symbol};
        } else {
            $name = "$job->{job_group_symbol}($job->{job_type_symbol})";
        }
        if ($job->{state} eq 'completed') {
            $status = $job->{result};
        } else {
            $status = $job->{state};
        }
        push @{$j{$status}}, $name;
    }
    return \%j;
}

sub sort_uniq {
    my %k = map { $_ => 1 } @_;
    return sort keys %k;
}

sub cmd_tryresults {
    cmd_help('try-results') unless @_ <= 1;
    my ($mb, $ffver, $tbver) = get_versions;
    my @branches = @_ ? @_ :
        $mb ? branches_list("heads", $ffver, $tbver) : (git_current_branch);
    my %notry = map { $_ => 1, "$ffver/$_" => 1 }
                git_notes_get_option('try-push', "$ffver/base", 'notry_branch')
                unless @_;
    foreach my $branch (sort @branches) {
        my $details = '';
        if ($options{details}) {
            my ($out, $err, $success) = capture_exec('git', 'log',
                '--no-merges', '--format=  %h %s', "$ffver/base..$branch");
            $details = join("\n", map { "  *$_" } split("\n", $out));
            $details .= "\n";
        }
        my $trypush = git_notes_get('try-push', $branch);
        if (($branch eq "$ffver/base.try" || $notry{$branch} ||
                git_notes_get_option('try-push', $branch, 'notry'))
                && !$options{failed}) {
            next if $options{failed};
            next if $options{success};
            print "$branch:\n$details  Will not be submitted\n";
        } elsif (my @pushid = git_notes_get_option('try-push', $branch, 'pushid')) {
            my ($id) = @pushid;
            my $jobs = treeherder_jobs_by_status(treeherder_get_jobs($id));
            if ($options{failed} && join('', keys %$jobs) eq 'success') {
                next;
            }
            if ($options{success} && join('', keys %$jobs) ne 'success') {
                next;
            }
            print "$branch:\n$details  https://treeherder.mozilla.org/#/jobs?repo=try&revision=$id\n";
            foreach my $status (sort keys %$jobs) {
                next if $status eq 'success';
                print "  $status: ", join(', ', sort_uniq(@{$jobs->{$status}})), "\n";
            }
        } else {
            next if $options{failed};
            next if $options{success};
            print "$branch:\n$details  Not submitted yet\n";
        }
        print "\n";
    }
}

sub cmd_trypush {
    my ($mb, $ffver, $tbver) = get_versions;
    my $max_push = $options{'max-push'} ? $options{'max-push'} : 1;
    my $nb_push = 0;
    my @branches = $mb ? branches_list("heads", $ffver, $tbver)
                       : (git_current_branch);
    my %notry = map { $_ => 1, "$ffver/$_" => 1 }
                git_notes_get_option('try-push', "$ffver/base", 'notry_branch');
    foreach my $branch (sort @branches) {
        next if $branch eq "$ffver/base.try";
        next if $notry{$branch};
        next if git_notes_get_option('try-push', $branch, 'pushid');
        next if git_notes_get_option('try-push', $branch, 'notry');
        if ($options{list}) {
            print "$branch\n";
            next;
        }
        exit 0 if $nb_push >= $max_push;
        if ($options{yes} || ask_yn("Push $branch to try?", 'y')) {
            my $try_branch = "$ffver/base.try" if branch_exists("$ffver/base.try");
            push_to_try($branch, "$ffver/base", $try_branch);
            $nb_push++;
        }
    }
}

sub cmd_tryconfig {
    cmd_help('try-config') unless keys %options == 1;
    cmd_help('try-config') if @_ > 1;
    my @gnotes = qw(git notes --ref try-push);
    my $rev = $_[0] // 'HEAD';
    return system(@gnotes, 'show', $rev) == 0 if $options{show};
    return system(@gnotes, 'edit', $rev) == 0 if $options{edit};
    return system(@gnotes, 'remove', $rev) == 0 if $options{remove};
    return system(@gnotes, 'append', $rev, '-m', $options{add});
}

sub git_autosquash {
    my ($base) = @_;
    my $old_git_editor = $ENV{GIT_EDITOR} if $ENV{GIT_EDITOR};
    $ENV{GIT_EDITOR} = ':';
    my @res = capture_exec('git', 'rebase', '-i', '--preserve-merges',
                           '--autosquash', $base);
    if (defined $old_git_editor) {
        $ENV{GIT_EDITOR} = $old_git_editor;
    } else {
        delete $ENV{GIT_EDITOR};
    }
    return @res;
}

sub rebase_abort {
    capture_exec('git', 'rebase', '--abort') if -d git_dir . '/rebase-merge';
    capture_exec('git', 'cherry-pick', '--abort')
                if -f git_dir . '/CHERRY_PICK_HEAD';
    capture_exec('git', 'merge', '--abort') if -f git_dir . '/MERGE_HEAD';
    my $rebase_branch = read_file(git_dir . '/tbgit_rebase_branch');
    my @files = qw(branch commit dst_ffver src_ffver);
    for my $f (@files) {
        unlink git_dir . "/tbgit_rebase_$f";
    }
    if (ask_yn("Remove branch $rebase_branch ?")) {
        run_noerr_noout('git', 'checkout', '--detach');
        run_noerr_noout('git', 'branch', '-D', $rebase_branch);
    }
}

sub rebase_continue {
    my @files = qw(branch commit dst_ffver src_ffver);
    foreach my $f (@files) {
        my $file = git_dir . "/tbgit_rebase_$f";
        exit_error "Can't open $file" unless -f $file;
    }
    my $current_branch = read_file(git_dir . '/tbgit_rebase_branch');
    exit_error "Unexpected branch" unless git_current_branch eq $current_branch;
    my $commit = read_file(git_dir . '/tbgit_rebase_commit');
    my $dst_ffver = read_file(git_dir . '/tbgit_rebase_dst_ffver');
    my $src_ffver = read_file(git_dir . '/tbgit_rebase_src_ffver');
    exit_error 'You should run git rebase --continue'
                if -d git_dir . '/rebase-merge';
    exit_error 'You should run git cherry-pick --continue'
                if -f git_dir . '/CHERRY_PICK_HEAD';
    exit_error 'You should run git commit'
                if -f git_dir . '/MERGE_HEAD';
    my $new_hash = git_hash('HEAD');
    print "Marking $new_hash as rebased from $commit\n";
    git_notes_append('rebase', $commit, "$dst_ffver=$new_hash");
    for my $f (@files) {
        unlink git_dir . "/tbgit_rebase_$f";
    }
    print "Restarting rebase of $current_branch\n";
    my ($name) = $current_branch =~ m/\/([^\/]+)$/;
    run_noerr_noout('git', 'checkout', '--detach');
    run_noerr_noout('git', 'branch', '-D', $current_branch);
    rebase_branch($dst_ffver, $src_ffver, $name);
}

sub pick_commit {
    my ($commit, $ffver, $src_ffver) = @_;
    my @r = git_notes_get_option('rebase', $commit, $ffver);
    if (@r) {
        return $r[0] if $r[0] eq 'skip';
        my ($out, $err, $success) = capture_exec('git', 'reset', '--hard', $r[0]);
        exit_error "Error picking rebased commit $r[0]: $err" unless $success;
        system('git', 'show', '-s', '--oneline');
        return $r[0];
    }
    my $prev_hash = git_hash('HEAD');
    my @parents = git_commit_parents($commit);
    my ($out, $err, $success) = (undef, undef, 1);
    my $action;
    my $current_branch = git_current_branch;
    if (@parents == 1) {
        $action = 'cherry-picking';
        ($out, $err, $success) = capture_exec('git', 'cherry-pick', '--ff',
                                                 '--allow-empty', $commit);
    } else {
        $action = 'merging';
        @r = git_notes_get_option('rebase', $parents[1], $ffver);
        ($out, $err, $success) = capture_exec('git', 'merge', '--no-edit',
                                        '--log', $r[0]) if $r[0] ne 'skip';
    }
    if ($success && $action eq 'cherry-picking') {
        $action = 'squashing';
        ($out, $err, $success) = git_autosquash("$ffver/base");
    }
    if (!$success) {
        write_file(git_dir . '/tbgit_rebase_commit', $commit);
        write_file(git_dir . '/tbgit_rebase_dst_ffver', $ffver);
        write_file(git_dir . '/tbgit_rebase_src_ffver', $src_ffver);
        write_file(git_dir . '/tbgit_rebase_branch', $current_branch);
        print STDERR "Error $action $commit: $out\n$err\n";
        exit_error "After fixing the conflict, run tbgit rebase --continue";
    }
    my $new_hash = git_hash('HEAD');
    $new_hash = 'skip' if $prev_hash eq $new_hash;
    git_notes_append('rebase', $commit, "$ffver=$new_hash");
    system('git', 'show', '-s', '--oneline');
    return $new_hash;
}

sub rebase_branch {
    if (-f git_dir . '/tbgit_rebase_commit') {
        exit_error 'Rebase already in progress. You can abort it with '
                   . 'tbgit rebase --abort.';
    }
    my ($dst_ffver, $src_ffver, $name) = @_;
    my ($dst, $dst_base, $src, $src_base)
        = ("$dst_ffver/$name", "$dst_ffver/base",
            "$src_ffver/$name", "$src_ffver/base");
    my ($out, $err, $success) = capture_exec('git', 'log', '--first-parent',
                                          '--format=%H', "$src_base..$src");
    exit_error "Error getting $src_base..$src: $err" unless $success;
    my @commits = reverse split("\n", $out);
    foreach my $commit (@commits) {
        my @parents = git_commit_parents($commit);
        next if @parents == 1;
        exit_error "More than 2 parents in unsupported" if @parents > 2;
        next if git_notes_get_option('rebase', $parents[1], $dst_ffver);
        print "Can't rebase $src: $parents[1] needs to be rebased first\n";
        return;
    }
    ($out, $err, $success) = capture_exec('git', 'branch', $dst, $dst_base);
    exit_error "Error creating branch $dst: $err" unless $success;
    ($out, $err, $success) = capture_exec('git', 'checkout', $dst);
    exit_error "Error checking out $dst: $err" unless $success;
    foreach my $commit (@commits) {
        pick_commit($commit, $dst_ffver, $src_ffver);
    }
    capture_exec('git', 'checkout', $exit_branch);
    print "Finished rebasing $src to $dst\n";
}

sub cmd_rebase {
    return rebase_continue if $options{continue};
    return rebase_abort if $options{abort};
    cmd_help('rebase') unless @_ == 1;
    my ($mb, $ffver, $tbver) = get_versions;
    exit "This command should be run from a merge branch" unless $mb;
    my %dest_branches = map { $_ => 1 } branches_list("heads", $ffver);
    my @src_branches = branches_list("heads", $_[0]);
    $exit_branch = git_current_branch;
    foreach my $branch (@src_branches) {
        my ($name) = $branch =~ m/^.+\/(.+)/;
        next if $dest_branches{"$ffver/$name"};
        if ($options{list}) {
            print "$name\n";
            next;
        }
        if (!$options{yes} && !ask_yn("Rebase $branch to $ffver/$name ?", 'y')) {
            next;
        }
        rebase_branch($ffver, $_[0], $name);
    }
}

sub cmd_list_branches {
    cmd_help('list-branches') unless @_ == 0;
    my (undef, $ffver, $tbver) = get_versions;
    my @branches = branches_list("heads", $ffver, $tbver);
    foreach my $branch (sort @branches) {
        next if $branch eq "$ffver/base";
        print "$branch:\n";
        system('git', 'log', '--format=  %h %s', "$ffver/base..$branch");
        print "\n";
    }
}

if (@ARGV && $actions{$ARGV[0]}) {
    my $action = shift @ARGV;
    Getopt::Long::GetOptionsFromArray(\@ARGV, \%options, 'help', @{$actions{$action}->{options}});
    cmd_help($action) if $options{help};
    $actions{$action}->{cmd}(@ARGV);
} else {
    cmd_help('tbgit');
}
